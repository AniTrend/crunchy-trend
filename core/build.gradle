plugins {
    id("com.android.library")
    id("kotlin-android")
    id("kotlin-android-extensions")
}

android {
    compileSdkVersion compileSdk
    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode versionCode
        versionName versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        def secretsProps = new Properties()
        file(".config/secrets.properties").withInputStream {
            stream -> secretsProps.load(stream)
        }
        it.buildConfigField("String", "clientToken", secretsProps['clientToken'])

        def configProps = new Properties()
        file(".config/configuration.properties").withInputStream {
            stream -> configProps.load(stream)
        }
        it.buildConfigField("String", "apiUrl", configProps['apiUrl'])
        it.buildConfigField("String", "apiAuthUrl", configProps['apiAuthUrl'])
        it.buildConfigField("String", "apiVersion", configProps['apiVersion'])
        it.buildConfigField("String", "apiExtension", configProps['apiExtension'])

        it.buildConfigField("int", "DATABASE_SCHEMA_VERSION", DATABASE_SCHEMA_VERSION)
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /** Project Modules */
    implementation project(":support-core")
    implementation project(":support-ext")

    /** Koin AndroidX Dependency Injection */
    implementation "org.koin:koin-core:$koin"
    implementation "org.koin:koin-core-ext:$koin"
    implementation "org.koin:koin-androidx-scope:$koin"
    implementation "org.koin:koin-androidx-viewmodel:$koin"

    /** Kotlin Libraries */
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesCore"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesAndroid"

    /** Material Design */
    implementation "com.google.android.material:material:$material"

    /** Architecture Components */
    implementation "androidx.lifecycle:lifecycle-extensions:$anroidx"
    implementation "android.arch.work:work-runtime:$worker"
    implementation "androidx.room:room-runtime:$anroidx"

    /** Retrofit Libraries */
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpLogger"

    /** Gson */
    implementation "com.google.code.gson:gson:$gson"

    /** Timber */
    implementation "com.jakewharton.timber:timber:$timber"

    /** Testing-only dependencies */
    testImplementation "junit:junit:$junit"
    androidTestImplementation "androidx.test:runner:$runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
}
